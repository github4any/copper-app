plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow' version '4.0.4'
  id 'com.bmuschko.docker-remote-api' version '6.7.0'
  id 'eclipse'
  id 'idea'
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

group 'copper.co'

if (project.hasProperty('projVersion')) {
  project.version = project.projVersion
} else {
  project.version = '0.0.1'
}

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
  jcenter()
  mavenCentral()  
}

dependencies {
  implementation 'log4j:log4j:1.2.17'
  implementation 'org.slf4j:slf4j-api:1.7.30'
  implementation 'org.slf4j:log4j-over-slf4j:1.7.30'
  implementation 'ch.qos.logback:logback-core:1.2.3'
  implementation 'ch.qos.logback:logback-classic:1.2.3'

  implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
  
  implementation 'com.google.code.gson:gson:2.7'
  implementation 'com.squareup.okhttp:okhttp:2.7.5'
  implementation 'org.apache.commons:commons-lang3:3.12.0'

  implementation 'io.undertow:undertow-core:2.0.20.Final'
  implementation 'io.undertow:undertow-servlet:2.0.20.Final'

  testCompile 'junit:junit:4.13'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

compileJava {
   options.compilerArgs << '-XDignore.symbol.file'
   options.fork = true
   options.forkOptions.executable = 'javac'
}

test {
  useJUnitPlatform()
  testLogging {
    events              'passed', 'skipped', 'failed'
    exceptionFormat     'full'
    showStandardStreams project.hasProperty('enableTestOutput')
  }
}

task dockerBuildImage(type: DockerBuildImage) {
  group = 'Docker'
  description = 'Produce Docker image'
  dependsOn build 
  inputDir = project.rootDir
  images.add("${project.group}/${project.name}:${project.version}")
}

application {
  mainClassName = 'co.copper.app.App'
}
  
jar {
  manifest {
    attributes(
      'Name': 'co/copper/app',
      'Specification-Title':  'App',
      'Specification-Version': "$project.version",
      'Specification-Vendor': 'Copper',
      'Implementation-Title': "$project.version",
      'Implementation-Vendor': 'Copper',
      'Implementation-Version': "$project.version",
      'Built-By': System.properties['user.name'],
      'Build-Date': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
      'Created-By': "Gradle ${gradle.gradleVersion}",
      'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
      'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
      'Sealed': 'true'
    )
  }
}

defaultTasks 'build'
